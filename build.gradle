/*
 * Copyright (c) 2017, The Modern Way. All rights reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories { mavenCentral() }
    dependencies {
        classpath(group: 'org.ajoberstar', name: 'grgit', version: '1.7.2')
        classpath(group: 'commons-codec', name: 'commons-codec', version: '1.10')
        classpath(group: 'commons-logging', name: 'commons-logging', version: '1.2')
        classpath(group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.3')
        classpath(group: 'org.fusesource.jansi', name: 'jansi', version: '1.16')
        classpath(group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.4.12', classifier: 'indy')
    }
}

import org.apache.tools.ant.filters.FixCrLfFilter
import org.apache.tools.ant.filters.ReplaceTokens
import java.net.InetAddress
import org.ajoberstar.grgit.Grgit

apply plugin: 'pmd'
apply plugin: 'maven'
apply plugin: 'groovy'
apply plugin: 'jacoco'
apply plugin: 'signing'
apply plugin: 'eclipse'
apply plugin: 'findbugs'
apply plugin: 'checkstyle'

compileJava.options.encoding = 'UTF-8'

sourceCompatibility = 1.8

targetCompatibility = 1.8

version = '1.2.1-RELEASE'

group = 'com.themodernway'

task deleteConfigs(type: Delete) {
    FileTree tree = fileTree(dir: "${buildDir}/resources/main")
    tree.each { File file ->
        delete(file)
    }
}

task filterConfigs(dependsOn: deleteConfigs, type: Copy) {
    from sourceSets.main.allSource
    into "${buildDir}/resources/main"
    filter(FixCrLfFilter)
    filter(ReplaceTokens, tokens: getFilterTokens())
}

task doJavaDocs(type: Groovydoc) {
    source = sourceSets.main.allSource
    classpath = sourceSets.main.runtimeClasspath + buildscript.configurations.classpath
    destinationDir = file("${buildDir}/docs/javadoc")
    includes = ['com/themodernway/common/**']
    docTitle = "TheModernWay Common"
    footer = "<small><a href=\"${getBuildGITHomeURL()}\" target=\"_blank\">${getProjectCommonName()} - &copy; 2017</a> - <a href=\"${getBuildGITCommitHash()}\" target=\"_blank\">${getBuildModuleVersion()}</a> - <a href=\"mailto:deansjones@gmail.com\">Author: Dean S. Jones</a></small>"
    getGroovydocLinks().each { name, list ->
        link(name, *list)
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: doJavaDocs) {
    classifier = 'javadoc'
    from "${buildDir}/docs/javadoc"
}

checkstyle {
    toolVersion = "7.7"
    ignoreFailures = true
}

tasks.withType(Checkstyle) { source 'src/main' }

compileJava.dependsOn(clean)

jar.dependsOn(filterConfigs)

task codeCoverage << { println ":codeCoverage-done" }

codeCoverage.dependsOn jacocoTestReport

jacocoTestReport {
    reports {
        csv {
            enabled true
            destination "${buildDir}/reports/jacoco/coverage-${version}.csv"
        }
        xml.enabled false
        html.destination "${buildDir}/reports/coverage"
    }
}

jacocoTestReport.dependsOn test

test {
    jacoco {
        append = false
        destinationFile = file("${buildDir}/jacoco/testing.exec")
        classDumpFile = file("${buildDir}/jacoco/classpathdumps")
    }
}

pmd {
    ignoreFailures = true
    toolVersion = "5.6.1"
    ruleSets = [
        'java-basic',
        'java-braces',
        'java-empty',
        'java-imports',
        'java-optimizations',
        'java-strictexception',
        'java-strings',
        'java-typeresolution',
        'java-unusedcode'           
        ]
}
    
findbugs {
    ignoreFailures = true
    toolVersion = "3.0.1"
    effort = "max"
    excludeFilter = file("$rootProject.projectDir/config/findbugs/filter.xml")
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled false
        html.enabled true
    }
}

task checkCodeQuality << { println ":checkCodeQuality-done" }

checkCodeQuality.dependsOn codeCoverage,checkstyleMain,pmdMain,findbugsMain

jar {
    manifest {
        attributes 'Implementation-Title': getProjectTitle(), 'Implementation-Version': getBuildModuleVersion()
    }
}

def getFilterTokens() {
    [GRADLE_BUILD_ARTIFACT_ID: getProjectArtifactID(), GRADLE_BUILD_PROJECT_TITLE: getProjectTitle(), GRADLE_BUILD_USER: getBuildUser(), GRADLE_BUILD_DATE: getBuildDate(), GRADLE_JAVA_VERSION: getJavaVersion(), GRADLE_GIT_COMMIT_HASH: getBuildGITCommitHash(), GRADLE_GIT_COMMIT_USER: getBuildGITCommitUser(), GRADLE_GIT_COMMIT_MESSAGE: getBuildGITCommitMessage(), GRADLE_BUILD_SERVER_HOST: getBuildServerHost(), GRADLE_BUILD_SERVER_BUILD: getBuildServerBuild(), GRADLE_BUILD_MODULE_VERSION: getBuildModuleVersion()]
}

def String getProjectArtifactID() {
    getProjectTitle().toLowerCase()
}

def String getProjectTitle() {
    getProjectCommonName().replaceAll(' ', '-')
}

def String getProjectCommonName() {
    'TheModernWay Common'
}

def getBuildModuleVersion() {
    version
}

def getBuildDate() {
    (new Date()).toString()
}

def getBuildServerHost() {
    try {
        InetAddress.getLocalHost().getHostName()
    }
    catch (Exception e) {
        "localhost"
    }
}

def getJavaVersion() {
    System.getProperty('java.version') ?: 'unknown'
}

def getBuildUser() {
    System.getProperty('user.name') ?: 'unknown'
}

def getBuildServerBuild() {
    System.getenv('BUILD_DISPLAY_NAME') ?: 'Command line ' + GradleVersion.current().toString()
}

def getBuildGITHomeURL() {
    'https://github.com/themodernway/' + getProjectArtifactID()
}

def getBuildGITCommitHash() {
    getBuildGITHomeURL() + '/commit/' + Grgit.open(project.file('.')).head().id
}

def getBuildGITCommitUser() {
    Grgit.open(project.file('.')).head().committer.email
}

def getBuildGITCommitMessage() {
    Grgit.open(project.file('.')).head().shortMessage
}

repositories { mavenCentral() }

sourceSets {
    main {
        resources { source java }
    }
}

dependencies {
    compile(group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2')
    testCompile(group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.4.12', classifier: 'indy')
    testCompile(group: 'org.spockframework', name: 'spock-core', version: '1.1-groovy-2.4') {
        exclude(group: 'org.codehaus.groovy', module: 'groovy-all')
    }
    testCompile(group: 'log4j', name: 'log4j', version: '1.2.17')
    testCompile(group: 'log4j', name: 'apache-log4j-extras', version: '1.2.17')
    testCompile(group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25')
    testCompile(group: 'org.slf4j', name: 'jul-to-slf4j', version: '1.7.25')
    testCompile(group: 'org.slf4j', name: 'log4j-over-slf4j', version: '1.7.25')
    testCompile(group: 'commons-logging', name: 'commons-logging', version: '1.2')
}

artifacts {
    archives sourcesJar
    archives javadocJar
    archives jar
}

signing { sign configurations.archives }

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
            repository(url: "${sonatype_stageingURL}") {
                //repository(url: "${sonatype_snapshotURL}") {
                authentication(userName: "${sonatype_user_tmw}", password: "${sonatype_password_tmw}")
            }
            pom.project {
                name 'TheModernWay-Common'
                packaging 'jar'
                description 'TheModernWay-Common is common integfaces and utility classes for TheModernWay Projects.'
                url 'https://github.com/themodernway/themodernway-common'
                scm {
                    url 'scm:git@github.com:themodernway/themodernway-common.git'
                    connection 'scm:git@github.com:themodernway/themodernway-common.git'
                    developerConnection 'scm:git@github.com:themodernway/themodernway-common.git'
                }
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }
                developers {
                    developer {
                        id 'sprocketnyc'
                        name 'Dean S. Jones'
                        email 'deansjones@gmail.com'
                        url 'https://www.themodernway.com'
                    }
                }
            }
        }
    }
}

def Map<String, List<String>> getGroovydocLinks() {
    def lmap = [:]
    File data = file('groovydoc/GROOVYDOC_LINK_LISTS.txt')
    if (data.exists()) {
        data.eachLine { line ->
            def vals = ((CharSequence) line).tokenize(',')
            lmap[vals.head()] = vals.tail()
        }
    }
    lmap
}

task(buildGroovydocLinks) << {
    def site = [:]
    File data = file('groovydoc/GROOVYDOC_LINK_SITES.txt')
    if (data.exists()) {
        data.eachLine { line ->
            site[line] = ((CharSequence) new URL("${line}/package-list").text).tokenize('\n')
        }
    }
    File make = file('groovydoc/GROOVYDOC_LINK_LISTS.txt')
    if(make.exists()) {
        make.delete()
    }
    make.createNewFile()
    def save = make.newPrintWriter()
    site.each { link, list ->
        def show = "${link}"
        list.each { line -> show = "${show},${line}" }
        save.println("${show}")
    }
    save.flush()
    save.close()
}
